buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
}

apply plugin: 'java-library'

group 'org.comroid'
version '0' // actually build number

apply from: 'gradle/vars.gradle'

def exportedProjects = [
        ':core',

        ':spigot',

        ':catnip',
        ':discord4j',
        ':javacord',
        ':jda'
]

tasks.findByName('wrapper')?.configure {
    gradleVersion = '5.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

allprojects {
    tasks.withType(Javadoc) {
        options {
            encoding = 'UTF-8'
            links = [
                    "https://docs.oracle.com/javase/8/docs/api/",
                    "https://docs.oracle.com/javaee/7/api/",
                    "https://mewna.github.io/catnip/docs/",
                    //"https://www.javadoc.io/doc/com.discord4j/discord4j-core/3.0.9", // broken
                    "https://docs.javacord.org/api/v/3.0.4/"//,
                    //"https://ci.dv8tion.net/job/JDA/javadoc/" // broken
            ]
        }
    }
}

subprojects {
    apply plugin: 'java-library'

    group 'org.comroid'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = "sources"
    }

    task javadocJar(type: Jar) {
        //javadoc
        from javadoc.destinationDir
        archiveClassifier = "javadoc"
    }

    compileJava.options.encoding = 'UTF-8'
    repositories {
        maven { url 'https://maven.comroid.org' }
        mavenLocal()
        maven { url 'https://www.jitpack.io' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        jcenter()
    }

    dependencies {
        if (project.name != 'core')
            api project(rootProject.name == 'repo' ? ':cmdr:core' : ':core')

        implementation 'org.comroid:restless:0.15.2'
        implementation 'org.comroid:varbind:0.20.0'

        compileOnly 'org.jetbrains:annotations:21.+'
        implementation 'com.google.flogger:flogger:0.5.1'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.easymock:easymock:4.0.2'

        testCompileOnly 'org.codehaus.groovy:groovy:3.0.0-beta-3'
    }
}
/*
task allJavadoc(type: Javadoc) {
    source exportedProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(exportedProjects.collect { project(it).sourceSets.main.compileClasspath })
    options.encoding = 'UTF-8'
    options.destinationDirectory(file("./docs/"))
}*/

apply from: "$parent.projectDir/gradle/publishing.gradle"
